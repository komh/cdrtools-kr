dnl @(#)configure.in	1.2 00/06/02	Copyright 1998-2000 Heiko Eiﬂfeldt
dnl Process this file with autoconf to produce a configure script.
AC_INIT(cdda2wav.c)
AC_CONFIG_HEADER(lconfig.h)

dnl get canonical host
AC_CANONICAL_HOST

dnl extract cdrtools version from the cdrecord.c file
CDRTOOLS_VERSION=`sed -nf ../../version.sed <../../../cdrecord/cdrecord.c`
AC_SUBST(CDRTOOLS_VERSION)

dnl check for posix4 library on Solaris or SunOS
case "$host_os" in
   solaris*|sunos*)
AC_CHECK_LIB(posix4, sched_get_priority_max)
;;
esac
AC_CHECK_LIB(ossaudio, _oss_ioctl)

EXTRALIBS="$LIBS"

AC_SUBST(EXTRALIBS)

dnl check header files
AC_CHECK_HEADERS(sys/cdio.h)
AC_CHECK_HEADERS(sundev/srreg.h)
AC_CHECK_HEADERS(sys/audioio.h)
AC_CHECK_HEADERS(sun/audioio.h)
AC_CHECK_HEADERS(soundcard.h)
AC_CHECK_HEADERS(sys/soundcard.h)
AC_CHECK_HEADERS(linux/soundcard.h)
AC_CHECK_HEADERS(machine/soundcard.h)

case "${ac_cv_header_sys_cdio_h}""${ac_cv_header_sundev_srreg_h}" in
   *yes*)
HAVE_SUN_IOCTL=1
;;
   *)
HAVE_SUN_IOCTL=
;;
esac
AC_SUBST(HAVE_SUN_IOCTL)

case "${ac_cv_header_sys_audioio_h}""${ac_cv_header_sun_audioio_h}" in
   *yes*)
HAVE_SUNSOUND=1
;;
   *)
HAVE_SUNSOUND=
;;
esac
AC_SUBST(HAVE_SUNSOUND)

case "${ac_cv_header_machine_soundcard_h}""${ac_cv_header_sys_soundcard_h}""${ac_cv_header_linux_soundcard_h}" in
   *yes*)
HAVE_OSS=1
;;
   *)
HAVE_OSS=
;;
esac
AC_SUBST(HAVE_OSS)

dnl Checks for library functions.
AC_CHECK_FUNCS(strtoul)
AC_OUTPUT(local.cnf)
